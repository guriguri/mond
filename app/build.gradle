apply plugin: 'com.android.application'

android {
    compileSdkVersion Integer.parseInt(project.ANDROID_COMPILE_SDK_VERSION)
    buildToolsVersion project.ANDROID_BUILD_TOOLS_VERSION

    defaultConfig {
        applicationId "guriguri.mond"
        minSdkVersion Integer.parseInt(project.ANDROID_MIN_SDK_VERSION)
        targetSdkVersion Integer.parseInt(project.ANDROID_TARGET_SDK_VERSION)
        versionCode Integer.parseInt(project.ANDROID_VERSION_CODE)
        versionName project.ANDROID_VERSION_NAME
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            if ("${variant.buildType.name}" != "debug") {
                def newFile = project.APP_NAME + "-${variant.buildType.name}" + "-${variant.productFlavors.name}".replaceAll("[\\[\\]]", "");
                newFile += "-${defaultConfig.versionName}_" + new Date().format('yyMMdd_HHmm') + ".apk";
                output.outputFile = new File(output.outputFile.parent + "/apk", newFile);
            }
        }
    }

    buildTypes {
        release {
            try {
                runProguard false
            }
            catch (Exception e) {
                minifyEnabled false // gradle 2.1+
            }
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/DEPENDENCIES'
    }
    productFlavors {
        x86 {
            ndk {
                abiFilter "x86"
            }
        }
        arm {
            ndk {
                abiFilters "armeabi-v7a", "armeabi"
            }
        }
    }
    sourceSets.main {
        jniLibs.srcDir 'libs'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    // log
    compile 'com.github.tony19:logback-android-core:1.1.1-3'
    compile 'com.github.tony19:logback-android-classic:1.1.1-3'
    compile 'org.slf4j:slf4j-api:1.7.7'
    // sshd
    compile 'org.apache.sshd:sshd-core:0.8.0'
    compile 'org.bouncycastle:bcpg-jdk15on:1.51'
    compile 'org.apache.mina:mina-core:2.0.8'
    // util
    compile 'org.apache.commons:commons-lang3:3.3.2'
    // ssh client
    compile 'com.jcraft:jsch:0.1.51'
    // module
    compile project(':tunneling')
}
